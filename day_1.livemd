<!-- livebook:{"persist_outputs":true} -->

# Day 1

## Part One

```elixir
Mix.install([{:nx, github: "elixir-nx/nx", sparse: "nx"}])
```

```output
:ok
```

```elixir
{:ok, contents} = File.read("inputs/day_1.txt")

# Load input into an Nx.Tensor
input =
  contents
  |> String.split("\n", trim: true)
  |> Enum.map(fn x -> x |> String.trim() |> String.to_integer() end)
  |> Nx.tensor()
```

```output
#Nx.Tensor<
  s64[2000]
  [130, 142, 148, 147, 157, 160, 162, 160, 165, 164, 171, 169, 160, 165, 167, 195, 197, 193, 192, 188, 192, 190, 189, 193, 215, 214, 191, 189, 191, 199, 188, 191, 196, 200, 215, 175, 176, 187, 178, 169, 172, 168, 181, 180, 163, 173, 206, 192, 175, 177, ...]
>
```

```elixir
# We need to reshape into a 3D tensor so Nx.conv doesn't complain.
input
|> Nx.reshape({1, 1, Nx.size(input)})
# The operation will effectively calculate a sliding window of x[i] - x[i - 1]
# for i in 1..(Nx.size(input) - 1)
|> Nx.conv(Nx.tensor([[[-1, 1]]]), strides: [1])
# With this result, we can count all positive values, which
# means there was an increase in the value. Nx.greater will
# return a 1D Tensor of 0s and 1s and Nx.sum will count the
# 1 elements.
|> Nx.greater(0)
|> Nx.sum()
|> Nx.to_scalar()
```

```output
1167
```

## Part Two

```elixir
# For part 2, we need to use a sliding window of size 3
# for summing the input before we can apply the same process as above
input
|> Nx.window_sum({3})
|> Nx.reshape({1, 1, Nx.size(input) - 2})
|> Nx.conv(Nx.tensor([[[-1, 1]]]), strides: [1])
|> Nx.greater(0)
|> Nx.sum()
|> Nx.to_scalar()
```

```output
1130
```
